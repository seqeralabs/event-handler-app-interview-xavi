plugins {
    id 'groovy'
    id 'application'
    id 'jvm-test-suite'
}

group = 'io.seqera'
version = '0.0.1'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.apache.groovy:groovy:4.0.2'
    implementation 'org.apache.groovy:groovy-sql:4.0.2'
    implementation 'org.apache.groovy:groovy-yaml:4.0.2'
    implementation 'org.apache.groovy:groovy-json:4.0.2'
    implementation group: 'org.hsqldb', name: 'hsqldb', version: '2.7.2'

    testImplementation 'org.mockito:mockito-core:5.4.0'
    testImplementation 'org.mockito:mockito-junit-jupiter:5.4.0'
}

application {
    mainClass = 'App'
}

testing {
    suites {
        test {
            useJUnitJupiter('5.9.3')
        }
        integrationTest(JvmTestSuite) {
            useJUnitJupiter('5.9.3')
            dependencies {
                implementation project(':app')
                implementation 'org.apache.groovy:groovy:4.0.2'
            }
            targets {
                all {
                    testTask.configure {
                        shouldRunAfter(test)
                    }
                }
            }
        }
    }
}

tasks.named('check') {
    dependsOn(testing.suites.integrationTest)
}
